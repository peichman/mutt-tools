#!/usr/bin/perl -w
use strict;

use MIME::Parser;
use Email::Address;
use Getopt::Long;

GetOptions(
    'verbose|v'  => \my $VERBOSE,
    'template=s' => \my $TEMPLATE,
);

# for reading encoded email bodies
my $parser = MIME::Parser->new;
# create no temp files
$parser->tmp_to_core(1);
$parser->output_to_core(1);

my $HEADER = shift || 'From';

my $entity = $parser->parse(\*STDIN);
my $head = $entity->head;

for my $email (Email::Address->parse($head->get($HEADER))) {
    if ($TEMPLATE) {
        require Template;
        my $template = Template->new({ ABSOLUTE => 1 });
        $template->process($TEMPLATE, { email => $email })
            or die $template->error();
    } else {
        print $VERBOSE ? $email->format : $email->address;
        print "\n";
    }
}

# to use this to extract and add to LDAP
# extract --template ~/projects/mutt-tools/new.ldif | ldapvi --ldif --in --add

# to use this to extract and edit an LDAP entry by email address
# ldapvi "(mail=$(extract))"
