#!/usr/bin/perl -w
use strict;

=head1 NAME

ldif2muttquery

=head1 DESCRIPTION

Convert LDIF input (on STDIN) to the tabular format of mutt address queries.

=cut

use Net::LDAP::LDIF;

my $query = shift;
my $ldif = Net::LDAP::LDIF->new(\*STDIN);
my $found = 0;
my $results = '';

my @results;
while (!$ldif->eof) {
    my $entry = $ldif->read_entry;
    next unless $entry;
    my $cn = $entry->get_value('cn');
    for my $email ($entry->get_value('mail')) {
        push @results, {
            email => $email,
            cn    => $cn,
            extra => scalar($entry->get_value('displayName')),
        };
        $found++;
    }
}
if ($found) {
    my $entries = $found == 1 ? 'entry' : 'entries';
    print "Found $found matching $entries\n";
    print join("\n", map { format_result($_) } sort { $a->{cn} cmp $b->{cn} } @results);
} else {
    print "No entries with mail attributes matching '$query'\n";
    exit 1;
}

sub format_result {
    my $entry = shift;
    my $line = $entry->{email} . "\t" . $entry->{cn};
    $line .= "\t" . $entry->{extra} if $entry->{extra};
    return $line;
}

=head1 SEE ALSO

L<http://www.mutt.org/doc/manual/manual-4.html#query>

=cut
